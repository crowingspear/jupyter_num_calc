{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {
    "toc": "true"
   },
   "source": [
    "# Table of Contents\n",
    " <p><div class=\"lev1 toc-item\"><a href=\"#Mapleによる最小2乗法\" data-toc-modified-id=\"Mapleによる最小2乗法-1\"><span class=\"toc-item-num\">1&nbsp;&nbsp;</span>Mapleによる最小2乗法</a></div><div class=\"lev1 toc-item\"><a href=\"#最小2乗法の原理\" data-toc-modified-id=\"最小2乗法の原理-2\"><span class=\"toc-item-num\">2&nbsp;&nbsp;</span>最小2乗法の原理</a></div><div class=\"lev1 toc-item\"><a href=\"#chi^2の極小値から(2変数の例)\" data-toc-modified-id=\"chi^2の極小値から(2変数の例)-3\"><span class=\"toc-item-num\">3&nbsp;&nbsp;</span>$\\chi^2$の極小値から(2変数の例)</a></div><div class=\"lev1 toc-item\"><a href=\"#正規方程式(Normal-Equations)による解\" data-toc-modified-id=\"正規方程式(Normal-Equations)による解-4\"><span class=\"toc-item-num\">4&nbsp;&nbsp;</span>正規方程式(Normal Equations)による解</a></div><div class=\"lev2 toc-item\"><a href=\"#Mapleによる具体例\" data-toc-modified-id=\"Mapleによる具体例-41\"><span class=\"toc-item-num\">4.1&nbsp;&nbsp;</span>Mapleによる具体例</a></div><div class=\"lev1 toc-item\"><a href=\"#特異値分解(Singular-Value-Decomposition)による解\" data-toc-modified-id=\"特異値分解(Singular-Value-Decomposition)による解-5\"><span class=\"toc-item-num\">5&nbsp;&nbsp;</span>特異値分解(Singular Value Decomposition)による解</a></div><div class=\"lev1 toc-item\"><a href=\"#2次元曲面へのフィット\" data-toc-modified-id=\"2次元曲面へのフィット-6\"><span class=\"toc-item-num\">6&nbsp;&nbsp;</span>2次元曲面へのフィット</a></div><div class=\"lev2 toc-item\"><a href=\"#Mapleによる具体例\" data-toc-modified-id=\"Mapleによる具体例-61\"><span class=\"toc-item-num\">6.1&nbsp;&nbsp;</span>Mapleによる具体例</a></div><div class=\"lev2 toc-item\"><a href=\"#正規方程式による解法\" data-toc-modified-id=\"正規方程式による解法-62\"><span class=\"toc-item-num\">6.2&nbsp;&nbsp;</span>正規方程式による解法</a></div><div class=\"lev1 toc-item\"><a href=\"#課題\" data-toc-modified-id=\"課題-7\"><span class=\"toc-item-num\">7&nbsp;&nbsp;</span>課題</a></div>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "線形最小2乗法(LeastSquareFit)\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Mapleによる最小2乗法\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "前章では，データに多項式を完全にフィットする補間についてみた．今回は，近似的にフィットする最小二乗法について詳しくみていく．図のようなデータに直線をフィットする場合を考えよう．\n",
    "\n",
    "\n",
    "![C8_LeastSquareFitplot2d1.png](figs/C8_LeastSquareFitplot2d1.png)\n",
    "\n",
    "\n",
    "コマンドleastsquareによるfitting(2変数の例)\n",
    "```maple\n",
    "> restart: X:=[1,2,3,4]: Y:=[0,5,15,24]:\n",
    "> with(plots):with(linalg):with(stats):\n",
    "> l1:=pointplot(transpose([X,Y]),symbolsize=30):\n",
    "> eq_fit:=fit[leastsquare[[x, y], y = a0+a1*x, {a0,a1}]]([X, Y]);\n",
    "```\n",
    "$$\n",
    "eq\\_fit\\, := \\,y=-\\frac{19}{2}+{\\frac {41}{5}}\\,x\n",
    "$$\n",
    "```maple\n",
    "> f1:=unapply(rhs(eq_fit),x);\n",
    "```\n",
    "$$\n",
    "f1\\, := \\,x\\mapsto -\\frac{19}{2}+{\\frac {41}{5}}\\,x\n",
    "$$\n",
    "```maple\n",
    "> p1:=plot(f1(x),x=0..4):\n",
    "> display(p1,l1);\n",
    "```\n",
    "\n",
    "![C8_LeastSquareFitplot2d2.png](figs/C8_LeastSquareFitplot2d2.png)\n",
    "\n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 最小2乗法の原理\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "もっとも簡単な例で原理を解説する．近似関数として，\n",
    "\n",
    "$$\n",
    "F(x) = a_0+a_1\\,x\n",
    "$$\n",
    "という直線近似を考える．もっともらしい関数は$N$点の測定データとの差$d_i = F(x_i)-y_i$を最小にすればよさそうであるが，これはプラスマイナスですぐに消えて不定になる．そこで，\n",
    "\n",
    "$$\n",
    "\\chi^{2}=\\sum_i^N d_i^2=\\sum_i^N\\left(a_0+a_1\\,x_i-y_i\\right)^2\n",
    "$$\n",
    "という関数を考える．この$\\chi^2$(カイ二乗)関数が，$a_0, a_1$をパラメータとして変えた時に最小となる$a_0, a_1$を求める．これは，それらの微分がそれぞれ0となる場合である．これは$\\chi^2$の和$\\sum$(sum)の中身を展開し，\n",
    "\n",
    "|2=$|　　　　　　　　　　　　|\n",
    "|:----|:----|\n",
    "| |\n",
    "\n",
    "\n",
    "\n",
    "$a_0, a_1$でそれぞれ微分すれば\n",
    "\n",
    "|2 =$|　　　　　　　　　　　　|\n",
    "|:----|:----|:----|\n",
    "| |2 =$|　　　　　　　　　　　　|\n",
    "|| |\n",
    "\n",
    "\n",
    "という$a_0, a_1$を未知変数とする2元の連立方程式が得られる．これは前に説明した通り逆行列で解くことができる．\n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# $\\chi^2$の極小値から(2変数の例)\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "```maple\n",
    "> restart; X:=[1,2,3,4]: Y:=[0,5,15,24]: f1:=x->a0+a1*x:\n",
    "  S:=0: \n",
    "  for i from 1 to 4 do \n",
    "    S:=S+(f1(X[i])-Y[i])^2; \n",
    "  end do:\n",
    "> fS:=unapply(S,(a0,a1));\n",
    "```\n",
    "$$\n",
    "{\\it fS}\\, := \\,( {{\\it a0},{\\it a1}} )\\mapsto  \\left( {\\it a0}+{\\it a1} \\right) ^{2}+ \\left( {\\it a0}+2\\,{\\it a1}-5 \\right) ^{2}+ \\left( {\\it a0}+3\\,{\\it a1}-15 \\right) ^{2}+ \\left( {\\it a0}+4\\,{\\it a1}-24 \\right) ^{2}\n",
    "$$\n",
    "```maple\n",
    "> expand(fS(a0,a1));\n",
    "```\n",
    "$$\n",
    "4\\,{{\\it a0}}^{2}+20\\,{\\it a0}\\,{\\it a1}+30\\,{{\\it a1}}^{2}-88\\,{\\it a0}-302\\,{\\it a1}+826\n",
    "$$\n",
    "\n",
    "```maple\n",
    "> plot3d(fS(a0,a1),a0=-20..20,a1=0..20);\n",
    "```\n",
    "\n",
    "![C8_LeastSquareFitplot3d3.png](figs/C8_LeastSquareFitplot3d3.png)\n",
    "\n",
    "\n",
    "\n",
    "```maple\n",
    "> eqs:={diff(expand(S),a0)=0, diff(expand(S),a1)=0};\n",
    "```\n",
    "$$\n",
    "{\\it eqs}\\, := \\, \\left\\{ 8\\,{\\it a0}+20\\,{\\it a1}-88=0,20\\,{\\it a0}+60\\,{\\it a1}-302=0 \\right\\}\n",
    "$$\n",
    "\n",
    "```maple\n",
    "> solve(eqs,{a0,a1});\n",
    "```\n",
    "$$\n",
    "\\left\\{ {\\it a0}=-\\frac{19}{2},{\\it a1}={\\frac {41}{5}} \\right\\}\n",
    "$$\n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 正規方程式(Normal Equations)による解\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "より一般的な場合の最小二乗法の解法を説明する．先程の例では1次の多項式を近似関数とした．これをより一般的な関数，例えば，$\\sin, \\cos, \\tan, \\exp, \\sinh$などとする．これを線形につないだ関数を\n",
    "\n",
    "$$\n",
    "F \\left(x \\right)=a _{0}\\sin \\left(x \\right)+a _{1}\\cos \\left(x \\right)+a _{2}\\exp \\left(-x \\right)+a _{3}\\sinh \\left(x \\right)+\\cdots ={\\sum_{k=1}^{M}}a _{k }X _{k }\\left(x \\right)\n",
    "$$\n",
    "ととる．実際には，$X_k(x)$はモデルや，多項式の高次項など論拠のある関数列をとる．これらを基底関数(base functions)と呼ぶ．ここで線形といっているのは，パラメータ$a_k$について線形という意味である．このような，より一般的な基底関数を使っても，$\\chi^2$関数は\n",
    "\n",
    "$$\n",
    "{\\chi}^{2}=\\sum _{i=1}^{N} \\left( F \\left( x_{{i}} \\right) -y_{{i}} \\right) ^{2}\n",
    "=\\sum _{i=1}^{N} \\left( \\sum _{k=1}^{M}a_{{k}}X_{{k}} \\left( x_{{i}} \\right) -y_{{i}} \\right) ^{2}\n",
    "$$\n",
    "と求めることができる．この関数を，$a_k$を変数とする関数とみなす．この関数が最小値を取るのは，$\\chi^2$を$M$個の$a_k$で偏微分した式がすべて0となる場合であ\n",
    "る．これを実際に求めてみると，\n",
    "\n",
    "$$\n",
    "\\sum _{i=1}^{N} \\left( \\sum _{j=1}^{M}a_{{j}}X_{{j}} \\left( x_{{i}} \\right) -y_{{i}} \\right) X_{{k}} \\left( x_{{i}} \\right) =0\n",
    "$$\n",
    "となる．ここで，$k = 1..M$の$M$個の連立方程式である．この連立方程式を最小二乗法の正規方程式(normal equations)と呼ぶ．\n",
    "\n",
    "上記の記法のままでは，ややこしいので，行列形式で書き直す．$N \\times M$で，各要素を\n",
    "\n",
    "$$\n",
    "A_{ij} = X_j(x_i)\n",
    "$$\n",
    "とする行列$A$を導入する．この行列は，\n",
    "\n",
    "$$\n",
    "A=\\left[\n",
    "\\begin{array}{cccc}\n",
    "X_1(x_1) & X_2(x_1) & \\cdots & X_M(x_1) \\\\\n",
    "\\vdots & \\vdots & \\cdots & \\vdots \\\\\n",
    "\\vdots & \\vdots & \\cdots & \\vdots \\\\\n",
    "\\vdots & \\vdots & \\cdots & \\vdots \\\\\n",
    "X_1(x_N) & X_2(x_N) & \\cdots & X_M(x_N) \n",
    "\\end{array}\n",
    "\\right]\n",
    "$$\n",
    "となる．これをデザイン行列と呼ぶ．すると先程の正規方程式は，\n",
    "\n",
    "$$\n",
    "A^t . A . a = A^t . y\n",
    "$$\n",
    "で与えられる．$A^t$は行列$A$の転置(transpose)\n",
    "\n",
    "$$\n",
    "A^t = A_{ij}^t = A_{ji}\n",
    "$$\n",
    "を意味し，得られた行列は，$M \\times N$である．$a, y$はそれぞれ，\n",
    "\n",
    "$$\n",
    "a=\\left[\n",
    "\\begin{array}{c}\n",
    "a_1\\\\a_2\\\\\\vdots\\\\a_M\n",
    "\\end{array}\n",
    "\\right],\\,\n",
    "y=\\left[\n",
    "\\begin{array}{c}\n",
    "y_1\\\\y_2\\\\\\vdots\\\\y_N\n",
    "\\end{array}\n",
    "\\right]\n",
    "$$\n",
    "である．\n",
    "\n",
    "$M = 3, N = 25$として行列の次元だけで表現すると，\n",
    "\n",
    "$$\n",
    "\\left[\n",
    "\\begin{array}{ccccc}\n",
    "&  & \\cdots & &\\\\\n",
    "\\cdots & \\cdots &  \\cdots & \\cdots & \\cdots \\\\\n",
    "&  & \\cdots & &\\\\\n",
    "\\end{array}\n",
    "\\right]\n",
    "\\left[\n",
    "\\begin{array}{ccc}\n",
    "& \\vdots &\\\\\n",
    "& \\vdots &\\\\\n",
    "\\cdots & \\cdots &  \\cdots\\\\\n",
    "& \\vdots &\\\\\n",
    "& \\vdots &\\\\\n",
    "\\end{array}\n",
    "\\right]\n",
    "\\left[\n",
    "\\begin{array}{c}\n",
    "\\vdots\\\\\n",
    "\\vdots\\\\\n",
    "\\vdots\n",
    "\\end{array}\n",
    "\\right]\n",
    "=\n",
    "\\left[\n",
    "\\begin{array}{ccccc}\n",
    "&  & \\cdots & &\\\\\n",
    "\\cdots & \\cdots &  \\cdots & \\cdots & \\cdots \\\\\n",
    "&  & \\cdots & &\\\\\n",
    "\\end{array}\n",
    "\\right]\n",
    "\\left[\n",
    "\\begin{array}{c}\n",
    "\\vdots\\\\\n",
    "\\vdots\\\\\n",
    "\\vdots\\\\\n",
    "\\vdots\\\\\n",
    "\\vdots\n",
    "\\end{array}\n",
    "\\right]\n",
    "$$\n",
    "となる．これは少しの計算で$3 \\times 3$の逆行列を解く問題に変形できる．\n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Mapleによる具体例 \n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "```maple\n",
    "> restart; X:=[1,2,3,4]: Y:=[0,5,15,24]: \n",
    "  f1:=x->a[1]+a[2]*x+a[3]*x^2:\n",
    "  with(LinearAlgebra): Av:=Matrix(1..4,1..3):\n",
    "  ff:=(x,i)->x^(i-1):\n",
    "  for i from 1 to 3 do \n",
    "    for j from 1 to 4 do\n",
    "      Av[j,i]:=ff(X[j],i); \n",
    "    end do; \n",
    "  end do;\n",
    "  Av;\n",
    "```\n",
    "$$\n",
    "\\left[ \\begin{array}{ccc} 1&1&1\\\\1&2&4\\\\1&3&9\\\\1&4&16\\end {array} \\right]\n",
    "$$\n",
    "```maple\n",
    "> Ai:=MatrixInverse(Transpose(Av).Av);\n",
    "```\n",
    "$$\n",
    "{\\it Ai}\\, := \\, \\left[ \\begin {array}{ccc} \n",
    "{ \\frac {31}{4}}&-{ \\frac {27}{4}}& \\frac{5}{4}\\\\\n",
    "-{ \\frac {27}{4}}&{ \\frac {129}{20}}& -\\frac{5}{4}\\\\\n",
    "\\frac{5}{4}& -\\frac{5}{4}& \\frac{1}{4}\n",
    "\\end {array} \\right]\n",
    "$$\n",
    "```maple\n",
    "> b:=Transpose(Av).Vector(Y);\n",
    "```\n",
    "$$\n",
    "b\\, := \\, \\left[ \\begin {array}{c} 44\\\\151\\\\539\\end {array} \\right]\n",
    "$$\n",
    "\n",
    "```maple\n",
    "> Ai.b;\n",
    "```\n",
    "$$\n",
    "\\left[ \\begin {array}{c} -\\frac{9}{2}\\\\\n",
    "{\\frac {16}{5}}\\\\\n",
    "1\\end {array} \\right]\n",
    "$$\n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 特異値分解(Singular Value Decomposition)による解\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "正規方程式を解くときには，少し注意が必要である．正規方程式での共分散行列,特異値分解の導出や標準偏差との関係はNumRecipeを参照せよ．\n",
    "```maple\n",
    "> restart; X:=[1,2,3,4]: Y:=[0,5,15,24]: f1:=x->a[1]+a[2]*x+a[3]*x^2:\n",
    "> with(LinearAlgebra): Av:=Matrix(1..4,1..3):\n",
    "> ff:=(x,i)->x^(i-1): \n",
    "  for i from 1 to 3 do \n",
    "    for j from 1 to 4 do\n",
    "      Av[j,i]:=ff(X[j],i); \n",
    "    end do; \n",
    "  end do; \n",
    "  Av;\n",
    "```\n",
    "$$\n",
    "\\left[ \\begin {array}{ccc} 1&1&1\\\\1&2&4\\\\1&3&9\\\\1&4&16\\end {array} \\right] \n",
    "$$\n",
    "\n",
    "```maple\n",
    "> U,S,Vt:=evalf(SingularValues(Av,output=['U','S','Vt'])):\n",
    "> DiagonalMatrix(S[1..3],4,3); U.DiagonalMatrix(S[1..3],4,3).Vt:\n",
    "```\n",
    "$$\n",
    "\\left[ \\begin {array}{ccc}  19.6213640200000015&0&0\\\\0& 1.71206987399999999&0\\\\0&0& 0.266252879300000022\\\\0&0&0\\end {array} \\right]\n",
    "$$\n",
    "\n",
    "```maple\n",
    "> iS:=Vector(3): \n",
    "  for i from 1 to 3 do \n",
    "    iS[i]:=1/S[i]; \n",
    "  end do:\n",
    "> DiS:=DiagonalMatrix(iS[1..3],3,4);\n",
    "```\n",
    "$$\n",
    "{\\it DiS}\\, := \\, \\left[ \\begin {array}{cccc}  0.05096485642&0&0&0\\\\0& 0.5840883104&0&0\\\\0&0& 3.755827928&0\\end {array} \\right]\n",
    "$$\n",
    "\n",
    "```maple\n",
    "> Transpose(Vt).DiS.(Transpose(U).Vector(Y));\n",
    "```\n",
    "$$\n",
    "\\left[ \\begin {array}{c} - 4.50000000198176498\\\\ 3.20000000035008324\\\\ 1.00000000040565196\\end {array} \\right]\n",
    "$$\n",
    "\n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 2次元曲面へのフィット\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "先程の一般化をより発展させると，3次元$(x_i, y_i, z_i)$で提供されるデータへの，2次元平面でのフィットも可能となる．2次元の単純な曲面は，方程式を使って，\n",
    "\n",
    "$$\n",
    "F(x, y) = a_1+a_2\\,x+a_3\\,y+a_4\\,xy+a_5\\,x^2+a_6\\,y^2\n",
    "$$\n",
    "となる．デザイン行列の$i$行目の要素は，\n",
    "\n",
    "$$\n",
    "[1, x_i, y_i, x_i\\,y_i, x_i^2, y_i^2]\n",
    "$$\n",
    "として，それぞれ求める．このデータの変換の様子をMapleスクリプトで詳しく示した．後は，通常の正規方程式を解くようにすれば，このデータを近似する曲面を定めるパラメータ$a_1, a_2, \\cdots,a_6$が求まる．最小二乗法はパラメータ$a_k$について線形であればよい．\n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Mapleによる具体例 \n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "実際のデータ解析での例．データの座標をx,y,zで用意して，Mapleの埋め込み関数のleastsquareでfitしている．\n",
    "```maple\n",
    "> with(plots):with(plottools): \n",
    "  z:=[0.000046079702088, 0.000029479057275,\n",
    "  0.000025769637830, 0.000034951410953, 0.000057024385455, 0.000029485453808,\n",
    "  0.000011519913869, 0.000006442404299, 0.000014252898382, 0.000034951410953,\n",
    "  0.000025769637773, 0.000006442404242, 0.000000000000057, 0.000006442404242,\n",
    "  0.000025769637773, 0.000034932221524, 0.000014246501905, 0.000006442404299,\n",
    "  0.000011519913926, 0.000029479057332, 0.000056973214100, 0.000034932221467,\n",
    "  0.000025769637773, 0.000029485453808, 0.000046079702031]:\n",
    ">  x:=[]:\n",
    "  y:=[]:\n",
    "  p1:=2:\n",
    "  for i from -p1 to p1 do\n",
    "    for j from -p1 to p1 do\n",
    "      x:=[op(x),i*0.0005];\n",
    "      y:=[op(y),j*0.0005];\n",
    "    end do;\n",
    "  end do;\n",
    "> with(LinearAlgebra): p2:=convert(Transpose(Matrix([x,y,z])),listlist):\n",
    "  pp2:=pointplot3d(p2,symbol=circle,symbolsize=30,color=black):\n",
    "  with(stats): data:=[x,y,z]: \n",
    "  fit1:=fit[leastsquare[[t,s,u], \n",
    "    u=a1+a2*t+a3*s+a4*t*s+a5*t^2+a6*s^2, \n",
    "    {a1,a2,a3,a4,a5,a6}]](data);\n",
    "```\n",
    "$$\n",
    "{\\it fit1}\\, := \\,u=-{ 8.657142857\\times 10^{-13}}- 0.000006396456800\\,t+ 0.000006396438400\\,s\\notag \\\\ \n",
    "- 5.459553587\\,ts+ 25.76962838\\,{t}^{2}+ 25.76962835\\,{s}^{2} \\notag \n",
    "$$\n",
    "```maple\n",
    "> f1:=unapply(rhs(fit1),(s,t)):\n",
    "> pf1:=plot3d(f1(ss,uu),ss=-0.001..0.001,uu=-0.001..0.001,color=gray):\n",
    "> display(pf1,pp2,axes=boxed);\n",
    "```\n",
    "\n",
    "![C8_LeastSquareFitplot3d4.png](figs/C8_LeastSquareFitplot3d4.png)\n",
    "\n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 正規方程式による解法 \n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "デザイン行列へのデータ変換\n",
    "```maple\n",
    "> bb:=Vector(25): A:=Matrix(25,6): \n",
    "  p1:=2: \n",
    "  for i from 1 to 25 do \n",
    "    A[i,1]:=1;\n",
    "    A[i,2]:=x_i; \n",
    "    A[i,3]:=y_i; \n",
    "    A[i,4]:=x_i*y_i; \n",
    "    A[i,5]:=x_i^2; \n",
    "    A[i,6]:=y_i^2;\n",
    "    bb_i:=z_i; \n",
    "  end do:\n",
    "```\n",
    "正規方程式の解\n",
    "```maple\n",
    "> MatrixInverse(Transpose(A).A).(Transpose(A).bb);\n",
    "```\n",
    "$$\n",
    "\\left[ \\begin {array}{c} -{ 9.185257196\\times 10^{-13}}\\\\   - 0.00000639644675999994798\\\\    0.00000639644220000032532\\\\   - 5.45955358336000173\\\\    25.7696284050857187\\\\    25.7696284050857543\\end {array} \\right]\n",
    "$$\n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 課題\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "1.  1次元の線形最小二乗法\n",
    "\n",
    "次の4点のデータを$y = a_1+a_2 x+a_3 x^2$で近似せよ(2006年度期末試験)．\n",
    "```maple\n",
    "X:=[0,1,2,3];\n",
    "Y:=[1,3,4,10];\n",
    "```\n",
    "\n",
    "1.  2次元の最小二乗フィット\n",
    "\n",
    "以下のデータを\n",
    "\n",
    "$$\n",
    "f(x, y) = a_1+a_2 x+a_3 y+a_4 xy\n",
    "$$\n",
    "で近似せよ\n",
    "```maple\n",
    " x,  y,  z\n",
    "-1, -1,  2.00000\n",
    "-1,  0,  0.50000\n",
    "-1,  1, -1.00000\n",
    " 0, -1,  0.50000\n",
    " 0,  0,  1.00000\n",
    " 0,  1,  1.50000\n",
    " 1, -1, -1.00000\n",
    " 1,  0,  1.50000\n",
    " 1,  1,  4.00000\n",
    "```\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.6.1"
  },
  "latex_envs": {
   "LaTeX_envs_menu_present": true,
   "autocomplete": true,
   "bibliofile": "biblio.bib",
   "cite_by": "apalike",
   "current_citInitial": 1,
   "eqLabelWithNumbers": true,
   "eqNumInitial": 1,
   "hotkeys": {
    "equation": "Ctrl-E",
    "itemize": "Ctrl-I"
   },
   "labels_anchors": false,
   "latex_user_defs": false,
   "report_style_numbering": false,
   "user_envs_cfg": false
  },
  "toc": {
   "colors": {
    "hover_highlight": "#DAA520",
    "navigate_num": "#000000",
    "navigate_text": "#333333",
    "running_highlight": "#FF0000",
    "selected_highlight": "#FFD700",
    "sidebar_border": "#EEEEEE",
    "wrapper_background": "#FFFFFF"
   },
   "moveMenuLeft": true,
   "nav_menu": {
    "height": "12px",
    "width": "252px"
   },
   "navigate_menu": true,
   "number_sections": true,
   "sideBar": true,
   "threshold": 4,
   "toc_cell": true,
   "toc_section_display": "block",
   "toc_window_display": true,
   "widenNotebook": false
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
